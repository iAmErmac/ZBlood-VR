
Class BloodWeaponZ : Weapon
{
	action void A_ReplaceSprite(String myvar, String ssprite, bool pickup=false, float sscale=0.7)
	{
		CVar readsetting = CVar.FindCVar(myvar);

		if (readsetting && readsetting.GetInt()==1)
		{
			if(pickup)
			{
				invoker.sprite = GetSpriteIndex(ssprite);
				invoker.A_SetScale(sscale);
			}
			else
			{
				if(player)
				{
					int mainhand = !invoker.bOffhandWeapon;
					if(mainhand)
						Player.GetPSprite(PSP_WEAPON).sprite = GetSpriteIndex(ssprite);
					else
						Player.GetPSprite(PSP_OFFHANDWEAPON).sprite = GetSpriteIndex(ssprite);
				}
			}
		}
	}
	
	action void A_HapticBuzz()
	{
		HapticHelper.BuzzWeapon(invoker);
	}
}

Class BloodFlameProjectile : Actor
{
	override int SpecialMissileHit(Actor victim)
	{
		if(Cvar.FindCvar("zblood_burnall").GetBool() && !isBloodEnemy(victim) && !victim.CountInv("PainBurnToken")) victim.SetInventory("MonsterBurnEffect", 1);
		
		Super.SpecialMissileHit(victim);
		return -1;
	}
	
	private bool isBloodEnemy(Actor mo)
	{
		Array<String> blood_enemies = {
			"bloodzombie", "bluecultist", "greycultist", "greencultist", "browncultist", "moron", "bloodrat", "bonefish", "bloatedbutcher", "chokinghand", "bracedhand", "chrysalidpod", "lavapod", "lavabarf", "hellrosebramblered", 
			"spidermother", "spidermother2", "spidermother3", "spiderred", "newspiderred", "newredspider", "greenspider", "spiderblue", "bluespider", "spideryellow", "yellowspider", "spiderpurple", "purplespider", "spidercyan", "spiderblack", "blackspider", "spiderwhite", "whitespider",
			"bloodgargoyle", "bloodgargoyle3", "cheogh", "statuecheogh", "statuegargoyle", "gillbeast", "hammerhaunt", "hellhound", "phantasm", "cerberus", "tchernobog"
		};
	
		String actor_class = mo.GetClassName();
		actor_class = actor_class.MakeLower();
		for(int i=0; i < blood_enemies.Size(); i++)
		{
			if(actor_class.IndexOf(blood_enemies[i]) != -1)
			{
				return true;
				break;
			}
		}
		return false;
	}
}

Class BloodAOEFlameProjectile : Actor
{
	override int SpecialMissileHit(Actor victim)
	{
		if(Cvar.FindCvar("zblood_burnall").GetBool()) markAreaBurnTargets();
		
		Super.SpecialMissileHit(victim);
		return -1;
	}
	
	action void A_MarkBurnVictims()
	{
		if(Cvar.FindCvar("zblood_burnall").GetBool()) invoker.markAreaBurnTargets();
	}
	
	void markAreaBurnTargets()
	{
		BlockThingsIterator it = BlockThingsIterator.Create(self, 320);
		Actor mo, victim;
		Array<Actor> victims;

		while (it.Next())
		{
			mo = it.thing; // Get the Actor it's currently on
			if (!mo || !mo.bSolid || Distance2D(mo) > 320 || !CheckSight(mo))
			   continue;

			victims.push(mo);
		}
	
		for(int i=0; i<victims.Size(); i++)
		{
			victim = victims[i];
			if(victim.CountInv("PainBurnToken")) return;
			
			victim.SetInventory("MonsterBurnEffect", 1);
		}
	}
}