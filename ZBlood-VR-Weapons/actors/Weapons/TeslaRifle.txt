Actor TeslaSmoke
{
	Height 1
	Radius 1
	PROJECTILE
	Damage 0
	Speed 1
	+RIPPER
	+BLOODLESSIMPACT
	Alpha 0.9
	Renderstyle add
	Scale 0.75
	States
	{
		Spawn:
			TSMK AABBCC 3 A_Fadeout(0.15)
		Stop
	}
}

Actor TeslaCounterX : Inventory
{
	inventory.amount 1
	inventory.maxamount 24
	+IGNORESKILL
}

Actor TeslaCharge : Ammo 
{
//$Category Blood-Ammo
//$Title Tesla Charge
    inventory.pickupmessage "Picked up a tesla charge"
    inventory.pickupsound "dsammoup"
	inventory.amount 32
	inventory.maxamount 250
    ammo.backpackamount 16
    ammo.backpackmaxamount 250	
	+IGNORESKILL
	Scale 0.4
	States
	{
		Spawn:
			TESC A -1
		Stop
	}
}

Actor TeslaRifle : BloodWeaponZ 12003
{
//$Category Blood Weapons
//$Title Tesla Rifle
	inventory.pickupmessage "Picked up Tesla Cannon"
	inventory.pickupsound "PICKWEAP"
	Obituary "%o was overcharged by %k."
	DamageType "Tesla"
	radius 20
	height 16
	Scale 0.8
    weapon.selectionorder 75
    weapon.kickback 4
    +NOGRAVITY
	weapon.ammotype1 "TeslaCharge"
	weapon.ammotype2 "TeslaCharge"
	weapon.ammouse1 1
	weapon.ammouse2 1
	weapon.ammogive 64
	Weapon.SlotNumber 8
	+IGNORESKILL
	+ThruGhost
	Tag "Tesla Rifle"
  
	States
	{
	Spawn:
		TSPI A -1
		Stop
  	Ready:
    	TESL A 0 A_ReplaceSprite("zblood_models", "TSLV")
    	"####" A 1 A_WeaponReady
    	Loop
	Select:
		TESL A 0 A_ReplaceSprite("zblood_models", "TSLV")
		"####" # 0 A_JumpIf(GetCVar("zblood_fastswitch") == 0, 2)
		"####" # 1 A_Raise(60)
		Wait
		"####" # 1 A_Raise
		Wait
	Deselect:
		TESL A 0 A_ReplaceSprite("zblood_models", "TSLV")
		"####" # 0 A_JumpIf(GetCVar("zblood_fastswitch") == 0, 2)
		"####" # 1 A_Lower(60)
		Wait
		"####" # 1 A_Lower
		Wait
  	Fire:
		TESL A 0 A_ReplaceSprite("zblood_models", "TSLV")
		"####" BC 1 BRIGHT A_WeaponReady(14)
		"####" # 0 BRIGHT A_GunFlash
		"####" # 0 BRIGHT A_PlayWeaponSound("TESLFIR")
		"####" # 0 A_FireCustomMissile("TeslaProjectile",0,1,5)
		"####" CD 1 BRIGHT A_WeaponReady(14)
		"####" # 0 A_JumpIfNoAmmo("Finish")
		"####" # 0 BRIGHT A_GunFlash
		"####" # 0 BRIGHT A_PlayWeaponSound("TESLFIR")
		"####" # 0 A_FireCustomMissile("TeslaProjectile",0,1,5)
		"####" DE 1 BRIGHT A_WeaponReady(14)
		"####" # 0 A_JumpIfNoAmmo("Finish")
		"####" # 0 BRIGHT A_GunFlash
		"####" # 0 BRIGHT A_PlayWeaponSound("TESLFIR")
		"####" # 0 A_FireCustomMissile("TeslaProjectile",0,1,5)
		"####" EF 1 BRIGHT A_WeaponReady(14)
		"####" # 0 A_JumpIfNoAmmo("Finish")
		"####" # 0 BRIGHT A_GunFlash
		"####" # 0 BRIGHT A_PlayWeaponSound("TESLFIR")
		"####" # 0 BRIGHT A_FireCustomMissile("TeslaProjectile",0,1,5)
		"####" # 0 A_JumpIfNoAmmo("Finish")
		"####" FB 1 BRIGHT A_Refire
		"####" # 0 A_FireCustomMissile("TeslaSmoke",random(-8,8),0,1,8+random(-.1,.1),0)
		"####" B 1 A_WeaponReady(14)
		Goto Ready
	Hold: // The Tesla Coil will stop if the player is not firing at this point
		TESL A 0 A_ReplaceSprite("zblood_models", "TSLV")
		"####" BC 1 BRIGHT A_WeaponReady(14)
		"####" # 0 BRIGHT A_GunFlash
		"####" # 0 BRIGHT A_PlayWeaponSound("TESLFIR")
		"####" # 0 A_FireCustomMissile("TeslaProjectile",0,1,5)
		"####" CD 1 BRIGHT A_WeaponReady(14)
		"####" # 0 A_JumpIfNoAmmo("Finish")
		"####" # 0 BRIGHT A_JumpIf (ACS_ExecuteWithResult(609)==0, "Finish")
		"####" # 0 BRIGHT A_GunFlash
		"####" # 0 BRIGHT A_PlayWeaponSound("TESLFIR")
		"####" # 0 A_FireCustomMissile("TeslaProjectile",0,1,5)
		"####" DE 1 BRIGHT A_WeaponReady(14)
		"####" # 0 A_JumpIfNoAmmo("Finish")
		"####" # 0 BRIGHT A_JumpIf (ACS_ExecuteWithResult(609)==0, "Finish")
		"####" # 0 BRIGHT A_GunFlash
		"####" # 0 BRIGHT A_PlayWeaponSound("TESLFIR")
		"####" # 0 A_FireCustomMissile("TeslaProjectile",0,1,5)
		"####" EF 1 BRIGHT A_WeaponReady(14)
		"####" # 0 A_JumpIfNoAmmo("Finish")
		"####" # 0 BRIGHT A_JumpIf (ACS_ExecuteWithResult(609)==0,"Finish")
		"####" # 0 BRIGHT A_GunFlash
		"####" # 0 BRIGHT A_PlayWeaponSound("TESLFIR")
		"####" # 0 BRIGHT A_FireCustomMissile("TeslaProjectile",0,1,5)
		"####" # 0 A_JumpIfNoAmmo("Finish")
		"####" # 0 BRIGHT A_JumpIf (ACS_ExecuteWithResult(609)==0, "Finish")
		"####" # 0 BRIGHT A_Refire
		Goto Finish
	Finish:
		TEXX A 0 A_ReplaceSprite("zblood_models", "TEXV")
		"####" # 0 A_GunFlash
		"####" # 0 A_FireCustomMissile("TeslaSmoke",random(-8,8),0,1,8+random(-.1,.1),0)			
		"####" ABC 2 A_WeaponReady(14)
		"####" # 0 A_GunFlash
		"####" # 0 A_ClearRefire
		Goto Ready
	Flash:
		//TSMK ABC 2
		TNT1 A 1 A_Light2
		TNT1 A 2 A_Light1
		Goto LightDone
	AltFire:
		TES2 A 0 A_ReplaceSprite("zblood_models", "TS2V")
		"####" # 0 A_JumpIfInventory("TeslaCharge", 35, 1)
		Goto Fire
		"####" # 0 A_Overlay(1001+random(1,10), "BoltFlash")
		"####" # 0 A_PlayWeaponSound("TESLWI") // 1.2 seconds (38 Tics)
		"####" ABCDE 3 A_WeaponReady(14)
		"####" # 0 A_HapticBuzz()
		"####" FGHIJ 3 A_WeaponReady(14)
		"####" # 0 A_HapticBuzz()
		"####" KLM 3 A_WeaponReady(14)
		"####" NOP 2 A_WeaponReady(14)
		"####" # 0 A_HapticBuzz()
		"####" QRSTU 2 A_WeaponReady(14)
		"####" # 0 A_HapticBuzz()
		"####" VWXYZ 2 A_WeaponReady(14)
		"####" # 0 A_HapticBuzz()
		TES3 A 0 A_ReplaceSprite("zblood_models", "TS3V")
		"####" ABCD 2 BRIGHT A_WeaponReady(14)
		TE2F A 0 A_ReplaceSprite("zblood_models", "T2FV")
		"####" # 0 BRIGHT A_PlayWeaponSound ("TESLWIX")
		"####" # 0 A_FireCustomMissile ("TeslaBallBig",0,1,5)
		"####" # 0 A_TakeInventory("TeslaCharge", 34)		
		"####" ABCDEF 1 BRIGHT A_WeaponReady(14)
    	TESL A 0 A_ReplaceSprite("zblood_models", "TSLV")
		"####" AAA 2 A_WeaponReady(14)
		Goto Ready
	BoltFlash:
		TNT1 A 0 A_JumpIf(GetCVar("zblood_models") == 1, "BoltFlash2")
		TSMK ABCDEFGHIJKLM 3 Bright A_Light1
		TSMK NOPQRSTUVWXYZ 2 Bright A_Light(2)
		TSM2 ABC 2 Bright A_Light(2)
		TSM2 D 2 Bright A_Light(5)
		TSM2 E 1 Bright A_Light(3)
		TNT1 A 1 A_Light0
		Stop
	BoltFlash2:
		TMKV ABCDEFGHIJKLM 3 Bright A_Light1
		TMKV NOPQRSTUVWXYZ 2 Bright A_Light(2)
		TM2V ABC 2 Bright A_Light(2)
		TM2V D 2 Bright A_Light(5)
		TM2V E 1 Bright A_Light(3)
		TNT1 A 1 A_Light0
		Stop
	Dummy:
		TSRV ABCDE 1
		TSLV BCDEFG 1
		TEXV ABC 1
		TS2V ABCDEFGHIJKLMNOPQRSTUVWXYZ 1
		TS3V ABCD 1
		T2FV ABCDE 1
		TMKV ABCDEFGHIJKLMNOPQRSTUVWXYZ 1
		TM2V ABC 1
		Stop
	}
}

Actor TeslaRifle2 : TeslaRifle
{
	Tag "Another Tesla Rifle"
	weapon.selectionorder 750
	weapon.ammogive 0
	+WEAPON.CHEATNOTWEAPON
	+WEAPON.OFFHANDWEAPON
	+WEAPON.NOHANDSWITCH
	+POWERED_UP //this tag is here to hide this weapon from weapon wheels
  
	States
	{
	Select:
		TESL A 0 A_ReplaceSprite("zblood_models", "TSLV")
		"####" # 1 A_Raise(100)
		Wait
	Deselect:
		TESL A 0 A_ReplaceSprite("zblood_models", "TSLV")
		"####" # 1 A_Lower(100)
		Wait
	}
}

Actor StopFiring : Inventory
{
	inventory.amount 1
	inventory.maxamount 1
}

Actor TeslaArc : Actor
{
	Radius 10
	Height 10
	Speed 0
	PainChance 0
	SeeSound ""
	Tag ""
	+GHOST
	+NOCLIP
	
	States
	{
	Spawn:
		TNT1 A 0
		TARC A 1
		Stop
	}
}