actor Blower
{
	PROJECTILE
	Speed 0
	+NOBLOCKMAP
	+DONTSPLASH
	+RIPPER
	+FLOORHUGGER
	+Bloodlessimpact
	renderstyle none
	gravity 10
	alpha .35	// 35 tics of constant AoE afterblast damage
	DamageType "Blast"	// players are affected less
	States
	{
		Spawn:
			TNT1 A 0
			MISL A 0 A_AlertMonsters
			"####" # 1
			MISL A 0 A_Explode(96,160,1)
			"####" # 1 A_Explode(96,140,1)
			"####" # 1 A_Explode(96,96,1)
			MISL A 0 A_ChangeFlag("EXTREMEDEATH",0)
		Damage:
			MISL A 1 A_Explode(2,768*alpha,1)
			MISL A 0 A_FadeOut(.01)
			Goto Damage
	}
}

Actor Blower2 : Blower
{
	States
	{
		Spawn:
			TNT1 A 0
			MISL A 0 A_AlertMonsters
			MISL A 0 A_Explode(128,128,1)
			"####" # 1 A_Explode(60,128,1)
			"####" # 1 A_Explode(60,128,1)
			MISL A 0 A_ChangeFlag("EXTREMEDEATH",0)
		Damage:
			MISL A 1 A_Explode(2,768*alpha,1)
			MISL A 0 A_FadeOut(.01)
			Goto Damage
	}
}

actor Kaboom : BulletPuff
{
  radius 1
  height 10
    +NOBLOCKMAP
  -NOGRAVITY  
  Decal Scorch
  renderstyle Add
  alpha 0.65
  +NOGRAVITY  
  +DONTSPLASH
  +RANDOMIZE
  states
  {
  Spawn:
    MISL B 0
    goto See
  See:
    MISL B 0 Radius_quake(2,14,0,16,0)
  	MISL B 0 A_JumpIf(waterlevel>2,"Vapor")
	MISL B 0 A_SpawnItemEx("KaboomDouble",0,0,0,momx,momy,momz,0,SXF_NOCHECKPOSITION)
    goto Death
  Vapor:
    MISL AAAAAA 0 A_SpawnItemEx("SmokeTrailIn4OutSlow",0,0,0,random[Puffs](random[Puffs](-4,0),random[Puffs](0,4)),random[Puffs](random[Puffs](-4,0),random[Puffs](0,4)),random[Puffs](2,4))
  Death:
    MISL BBBBB 0 A_CustomMissile("NapalmDebris", 0, 0, random[Puffs](0,360), 2, random[Puffs](-90,90))    
    MISL BBBBB 0 A_SpawnDebris ("NapalmDebris") 
    MISL B 0 A_Jump(128,"AltDeath")
    MISL BCD 3 bright    
    MISL D 0 Radius_Quake (2,16,0,16,0)
	MISL A 0 A_JumpIf(waterlevel>2,"DeathVapor")
    MISL EFGHI 3 bright
    MISL JKL 2 bright
    MISL MN 2 bright
    stop
  DeathVapor:
    MISL EFGHI 3 bright A_SpawnItemEx("SmokeTrailIn4OutSlow",0,0,0,random[Puffs](random[Puffs](-4,0),random[Puffs](0,4)),random[Puffs](random[Puffs](-4,0),random[Puffs](0,4)),random[Puffs](2,4))
    MISL JKL 2 bright A_SpawnItemEx("SmokeTrailIn4OutSlow",random[Puffs](-10,10),random[Puffs](-10,10),15,random[Puffs](random[Puffs](-4,0),random[Puffs](0,4)),random[Puffs](random[Puffs](-4,0),random[Puffs](0,4)),random[Puffs](2,4))
    MISL MN 2 bright A_SpawnItemEx("SmokeTrailIn4OutSlow",random[Puffs](-20,20),random[Puffs](-20,20),30,random[Puffs](random[Puffs](-4,0),random[Puffs](0,4)),random[Puffs](random[Puffs](-4,0),random[Puffs](0,4)),random[Puffs](2,4))
    stop
  AltDeath:
    MISL OPQ 3 bright
    MISL Q 0 bright Radius_Quake (2,16,0,16,0)
	MISL A 0 A_JumpIf(waterlevel>2,"AltDeathVapor")
    MISL RSTUV 3 bright 
    MISL WXY 2 bright
    MISL "Z[" 2 bright
    stop
  AltDeathVapor:
    MISL RSTUV 3 bright  A_SpawnItemEx("SmokeTrailIn4OutSlow",0,0,0,random[Puffs](random[Puffs](-4,0),random[Puffs](0,4)),random[Puffs](random[Puffs](-4,0),random[Puffs](0,4)),random[Puffs](2,4))
    MISL WXY 2 bright A_SpawnItemEx("SmokeTrailIn4OutSlow",random[Puffs](-10,10),random[Puffs](-10,10),15,random[Puffs](random[Puffs](-4,0),random[Puffs](0,4)),random[Puffs](random[Puffs](-4,0),random[Puffs](0,4)),random[Puffs](2,4))
    MISL "Z[" 2 bright A_SpawnItemEx("SmokeTrailIn4OutSlow",random[Puffs](-20,20),random[Puffs](-20,20),30,random[Puffs](random[Puffs](-4,0),random[Puffs](0,4)),random[Puffs](random[Puffs](-4,0),random[Puffs](0,4)),random[Puffs](2,4))
    stop
  }
}

actor KaboomDouble : Kaboom
{
  states
  {
  Spawn:
  	MISL A 0
	MISL B 0 A_Jump(128,"AltDeath")
    MISL BCD 1 bright    
	MISL E 2 bright A_JumpIf(waterlevel>2,"DeathVapor")
    MISL FGHI 2 bright
    MISL JKL 2 bright
    MISL MN 2 bright
    stop
  DeathVapor:
    MISL EFGHI 1 bright
    MISL JKL 1 bright
    MISL MN 1 bright 
    stop
  AltDeath:
    MISL OPQ 2 bright
	MISL R 2 bright A_JumpIf(waterlevel>2,"AltDeathVapor")
    MISL STUV 2 bright 
    MISL WXY 2 bright
    MISL "Z[" 2 bright
    stop
  AltDeathVapor:
    MISL RSTUV 1 bright
    MISL WXY 1 bright
    MISL "Z[" 1 bright 
    stop
  }
}

actor NapalmLauncher : BloodWeaponZ 12001 // 2003
{
//$Category Blood Weapons
//$Title Napalm Launcher
  spawnid 29
  Scale 0.6
  radius 20
  height 16
  inventory.pickupmessage "You got the Incinerator!"
  inventory.pickupsound "PICKWEAP"
  obituary "%k Blew %o with napalm"
  weapon.selectionorder 900
  weapon.kickback 240
  weapon.ammotype "GasolineCan"
  weapon.ammouse 1
  weapon.ammogive 6
  weapon.ammotype2 "GasolineCan"
  weapon.ammouse2 1
  //weapon.ammouse2 1
  +WEAPON.EXPLOSIVE
  +WEAPON.NOAUTOFIRE
  +NOGRAVITY
  Weapon.SlotNumber 5
  Tag "Napalm Launcher"
	
  states
  {
  Ready:
	NLAN A 0 A_ReplaceSprite("zblood_models", "NLNV")
	"####" A 1 A_WeaponReady
    loop
  Deselect:
	NLAN A 0 A_ReplaceSprite("zblood_models", "NLNV")
    "####" # 0 A_JumpIf(GetCVar("zblood_fastswitch") == 0, 2)
	"####" A 1 A_Lower(60)
	Wait
	"####" A 1 A_Lower
	Wait
  Select:
	NLAN A 0 A_ReplaceSprite("zblood_models", "NLNV")
    "####" # 0 A_JumpIf(GetCVar("zblood_fastswitch") == 0, 2)
	"####" A 1 A_Raise(60)
	Wait
	"####" A 1 A_Raise
	Wait
  Fire:
	"####" # 0 A_Overlay(1000+random(1,10), "Flash1")
    "####" F 0 A_PlaySound("dsnapalm")
    "####" F 0 A_FireCustomMissile ("NapalmRocket2", 0,1,5,3+random(-1,1))
	"####" F 5 bright A_WeaponReady(14)
    "####" GHI 4 bright A_WeaponReady(14)
    "####" I 0 A_ReFire
    goto Ready
  AltFire:
	"####" # 0 A_JumpIfInventory("GasolineCan", 12, 1)
	Goto Fire
	"####" # 0 A_TakeInventory("GasolineCan", 11)
	"####" # 0 A_Overlay(1000+random(1,10), "Flash1")
    "####" F 0 A_PlayWeaponSound("dsnaplm2")
    "####" F 0 A_FireCustomMissile ("NapalmRocketAlt1",random(-2,2),1,5,3+random(-2,2))
	"####" F 6 bright A_WeaponReady(14)
    "####" GHI 5 bright A_WeaponReady(14)
    "####" L 0 A_ReFire
    goto Ready
  Flash1:
	TNT1 A 0 A_JumpIf(GetCVar("zblood_muzzle_flash") == false, "NoFlashSprite")
	TNT1 A 0 A_JumpIf(GetCVar("zblood_models") == 1, "Flash2")
	NLFF A 3 Bright A_Light1
	NLFF B 2 Bright A_Light2
	NLFF C 2 Bright A_Light2
	NLFF D 2 Bright A_Light1
	NLFF E 2 Bright A_Light0
	NLFF FG 2 Bright
	Stop
  AltFlash:
	TNT1 A 0 A_JumpIf(GetCVar("zblood_muzzle_flash") == false, "NoAltFlashSprite")
	TNT1 A 0 A_JumpIf(GetCVar("zblood_models") == 1, "AltFlash2")
	NLFF A 3 Bright A_Light1
	NLFF B 3 Bright A_Light2
	NLFF C 3 Bright A_Light2
	NLFF D 2 Bright A_Light1
	NLFF E 2 Bright A_Light0
	NLFF FG 3 Bright
	Stop
  Flash2:
	NLVF A 3 Bright A_Light1
	NLVF B 2 Bright A_Light2
	NLVF C 2 Bright A_Light2
	NLVF D 2 Bright A_Light1
	NLVF E 2 Bright A_Light0
	NLVF FG 2 Bright
	Stop
  AltFlash2:
	NLVF A 3 Bright A_Light1
	NLVF B 3 Bright A_Light2
	NLVF C 3 Bright A_Light2
	NLVF D 2 Bright A_Light1
	NLVF E 2 Bright A_Light0
	NLVF FG 3 Bright
	Stop
  NoFlashSprite:
	TNT1 A 3 A_Light1
	TNT1 AA 2 A_Light2
	TNT1 A 2 A_Light1
	Goto LightDone
  NoAltFlashSprite:
	TNT1 A 3 A_Light1
	TNT1 AA 3 A_Light2
	TNT1 A 2 A_Light1
	Goto LightDone
  Spawn:
    NLAG A -1
    stop
  Dummy:
	NLNV ABCDEFGHI 1
	Stop
  }
}

//only used for akimbo
Actor NapalmLauncher2 : NapalmLauncher
{
  Tag "Another Submachinegun"
  weapon.selectionorder 9080
  weapon.ammogive 0
  +WEAPON.CHEATNOTWEAPON
  +WEAPON.OFFHANDWEAPON
  +WEAPON.NOHANDSWITCH
  +POWERED_UP //this tag is here to hide this weapon from weapon wheels
	
  states
  {
  Deselect:
	NLAN A 0 A_ReplaceSprite("zblood_models", "NLNV")
	"####" A 1 A_Lower(100)
	Wait
  Select:
	NLAN A 0 A_ReplaceSprite("zblood_models", "NLNV")
	"####" A 1 A_Raise(100)
	Wait
  }
}